% the facts to represent the given numbers are provided below
a(1,1,3). a(1,9,4).
a(2,4,6). a(2,6,9).
a(3,3,6). a(3,7,9).
a(4,2,8). a(4,4,3). a(4,6,2). a(4,8,6).
a(5,5,7).
a(6,2,1). a(6,4,8). a(6,6,5). a(6,8,7).
a(7,3,7). a(7,7,8).
a(8,4,7). a(8,6,8).
a(9,1,9). a(9,9,7).

% write the remaining part of the clingo program below
% Assign each number to each box
{ a(X,Y,N) : X=1..9, Y=1..9, X1<=X, X<=X1+2, Y1<=Y, Y<=Y1+2 } = 1
        :- N=1..9, X1=3*(0..2)+1, Y1=3*(0..2)+1.

% one value per square
:- a(X,Y,N1), a(X,Y,N2), N1!=N2.

% same number cant be assigned twice in one row
:- a(X,Y1,N), a(X,Y2,N), Y1!=Y2.

% same number cant be assigned twice in one column
:- a(X1,Y,N), a(X2,Y,N), X1!=X2.

% Knight distance
% A knight move is an L1 distance of 3 spaces away.
% We dont need to consider the case of the distance of 3 being strictly
% vertical or horizontal because of the rules above.
:- a(X1,Y1,N), a(X2,Y2,N), |X1-X2| + |Y1-Y2| == 3.
